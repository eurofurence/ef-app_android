apply plugin: "idea"
apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: 'kotlinx-serialization'
apply plugin: 'io.fabric'

android {

    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 78
        versionName "2.1.0"
        multiDexEnabled true
    }

    dexOptions {
        javaMaxHeapSize "4g"
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }

    packagingOptions {
        exclude "META-INF/DEPENDENCIES"
        exclude "META-INF/LICENSE"
        exclude "META-INF/MANIFEST.MF"
        exclude "META-INF/NOTICE"
        exclude "META-INF/maven/com.google.guava/guava/pom.xml"
        exclude "META-INF/maven/com.google.guava/guava/pom.properties"


    }

    defaultConfig {
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        androidTest.java.srcDirs += "src/androidTest/kotlin"
    }

    flavorDimensions "devProd"

    productFlavors {
        // Define separate dev and prod product flavors.
        dev {
            dimension "devProd"
            // dev utilizes minSDKVersion = 21 to allow the Android gradle plugin
            // to pre-dex each module and produce an APK that can be tested on
            // Android Lollipop without time consuming dex merging processes.
            minSdkVersion 25
            applicationId "org.eurofurence.connavigator.debug"
        }
        prod {
            dimension "devProd"
            // The actual minSdkVersion for the application.
            minSdkVersion 16
            applicationId "org.eurofurence.connavigator"
        }
    }
}


ext {
    // Location of the schema file
    schema_location = "https://app.eurofurence.org:40000/swagger/v2/swagger.json"

    // Dependency versions
    swagger_annotations_version = "1.5.0"
    gson_version = "2.6.1"
    anko_version = "0.10.1"
    httpcore_version = "4.4.4"
    httpmime_version = "4.5.2"
    volley_version = "1.0.0"
    junit_version = "4.8.1"
    android_support_version = "27.1.1"
    firebase_version = "12.0.1"
    fuel_version = "1.8.0"
}

// Embedded swagger
apply from: "swagger.gradle"

dependencies {
    testImplementation "junit:junit:4.12"
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$android_support_version"
    implementation "com.android.support:design:$android_support_version"
    implementation "com.android.support:support-v4:$android_support_version"
    implementation "com.android.support:cardview-v7:$android_support_version"
    implementation "com.android.support:recyclerview-v7:$android_support_version"
    implementation "com.android.support:cardview-v7:$android_support_version"
    implementation "com.android.support:recyclerview-v7:$android_support_version"
    implementation "com.android.support:multidex:1.0.3"
    implementation "net.danlew:android.joda:2.9.4.2"
    implementation "joda-time:joda-time:2.9.2:no-tzdb"
    implementation "io.swagger:swagger-annotations:$swagger_annotations_version"
    implementation "com.google.code.gson:gson:$gson_version"

    implementation "com.android.volley:volley:${volley_version}"
    implementation "com.nostra13.universalimageloader:universal-image-loader:1.9.5"

    implementation "org.apache.httpcomponents:httpcore:$httpcore_version"
    implementation("org.apache.httpcomponents:httpmime:$httpmime_version") {
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }

    // Firebase
    implementation "com.google.firebase:firebase-messaging:$firebase_version"
    implementation "com.google.firebase:firebase-config:$firebase_version"
    implementation "com.google.firebase:firebase-perf:$firebase_version"
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.4'

    // Photoview
    implementation 'com.github.chrisbanes:PhotoView:2.0.0'
    implementation 'us.feras.mdv:markdownview:1.1.0'

    // Promises
    implementation 'nl.komponents.kovenant:kovenant-core:3.3.0'
    implementation 'nl.komponents.kovenant:kovenant-android:3.3.0'
    implementation 'nl.komponents.kovenant:kovenant-combine:3.3.0'
    implementation 'nl.komponents.kovenant:kovenant-jvm:3.3.0'
    implementation 'nl.komponents.kovenant:kovenant-functional:3.3.0'

    // DSL for views
    implementation "org.jetbrains.anko:anko-sdk15:$anko_version" // sdk19, sdk21, sdk23 are also available
    implementation "org.jetbrains.anko:anko-appcompat-v7-coroutines:$anko_version"
    implementation "org.jetbrains.anko:anko-support-v4:$anko_version" // In case you need support-v4 bindings
    implementation "org.jetbrains.anko:anko-appcompat-v7:$anko_version" // For appcompat-v7 bindings
    implementation "org.jetbrains.anko:anko-coroutines:$anko_version"

    // Kandroid for easier components
    implementation 'com.pawegio.kandroid:kandroid:0.7.0@aar'

    //  Circular progress that don't make me consider suicide
    implementation 'com.github.lzyzsd:circleprogress:1.2.1'

    // Preferences
    implementation "com.chibatching.kotpref:kotpref:2.1.1"
    implementation "com.chibatching.kotpref:enum-support:2.1.1"

    // Fuel for networking
    implementation "com.github.kittinunf.fuel:fuel:$fuel_version" //for JVM
    implementation "com.github.kittinunf.fuel:fuel-android:$fuel_version" //for Android

    // Font awesome
    implementation 'com.joanzapata.iconify:android-iconify-fontawesome:2.2.1'

    // Observables
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'

    // Pretty time
    implementation "org.ocpsoft.prettytime:prettytime:4.0.1.Final"

    // Serialization
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$serialization_version"

    testImplementation "junit:junit:$junit_version"
}

apply plugin: "com.google.gms.google-services"
apply plugin: 'com.google.firebase.firebase-perf'
repositories {
    mavenCentral()
}
